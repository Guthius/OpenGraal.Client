//
// Name:   GO Police Net
// Author: Vangel (JF)
// Date:   6/27/99
//

if (washit && ((playeringuild GP) || (playeringuild Game Master))) {
  // game masters and GP can slash the net to get out or
  // to release somebody else out of the net.
  destroy;
}

if (playerenters) {
  timereverywhere;
  blockagain;
  drawoverplayer;
  curdistance=100;
  curplayer=-1;
  for (p=0;p<playerscount;p++) {
    pdistance=(((players[p].x-x)^2)+((players[p].y-y)^2))^0.5;
    if (pdistance<curdistance) {
      curplayer=p;
      curdistance=pdistance;
    }
  }
  if (curplayer==0) {
    if ((playeringuild GP) || (playeringuild Game Master)) {
      // don't trap game masters or GP
    } else {
      x=players[curplayer].x-1.5;
      y=players[curplayer].y-1.5;
    }
  }
  timeout=60;
  timershow;
}

if (playertouchsme) {
  if ((playeringuild GP) || (playeringuild Game Master)) {
    dontblock;
    drawoverplayer;
  } else {
    blockagain;
    drawoverplayer;
  }
}

if (timeout) destroy;